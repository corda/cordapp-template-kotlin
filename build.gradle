buildscript {
    ext {
        corda_release_group = 'net.corda'
        // Will revert to Corda 4.1 when it is available.
        corda_release_version = '5.0-SNAPSHOT'
        corda_gradle_plugins_version = '4.0.42'
        kotlin_version = '1.2.71'
        junit_version = '4.12'
        quasar_version = '0.7.10'
        spring_boot_version = '2.0.2.RELEASE'
        spring_boot_gradle_plugin_version = '2.0.2.RELEASE'
        slf4j_version = '1.7.25'
        log4j_version = '2.9.1'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url 'https://ci-artifactory.corda.r3cev.com/artifactory/corda' }    
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "net.corda.plugins:cordapp:$corda_gradle_plugins_version"
        classpath "net.corda.plugins:cordformation:$corda_gradle_plugins_version"
        classpath "net.corda.plugins:quasar-utils:$corda_gradle_plugins_version"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_gradle_plugin_version"
    }
}

allprojects {
    apply plugin: 'kotlin'

    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://ci-artifactory.corda.r3cev.com/artifactory/corda' }
        // Can be removed post-release - used to get nightly snapshot build.
        maven { url 'https://ci-artifactory.corda.r3cev.com/artifactory/corda-dev' }
        maven { url 'https://jitpack.io' }
        mavenLocal()
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
        kotlinOptions {
            languageVersion = "1.2"
            apiVersion = "1.2"
            jvmTarget = "1.8"
            javaParameters = true   // Useful for reflection.
        }
    }

    jar {
        // Don't include log config files.
        exclude "**/log4j2*.xml"
        // We need this because log4j2.xml is pulled in from multiple dependency JARs.
        // JAR signing will fail if there are multiple files with the same name.
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }
}

apply plugin: 'net.corda.plugins.cordapp'
apply plugin: 'net.corda.plugins.cordformation'
apply plugin: 'net.corda.plugins.quasar-utils'

sourceSets {
    main {
        resources {
            srcDir rootProject.file("config/dev")
        }
    }
}

dependencies {
    // Kotlin.
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // Corda dependencies.
    cordaCompile "$corda_release_group:corda-core:$corda_release_version"
    cordaCompile "$corda_release_group:corda-node-api:$corda_release_version"
    cordaRuntime "$corda_release_group:corda:$corda_release_version"

    // CorDapp dependencies.
    cordapp project(":workflows")
    cordapp project(":contracts")

    // For logging.
    testCompile "org.apache.logging.log4j:log4j-slf4j-impl:${log4j_version}"
    testCompile "org.apache.logging.log4j:log4j-web:${log4j_version}"

    // Token SDK dependencies.
    cordapp "com.r3.corda.sdk.token:token-sdk-workflow:0.1"
    cordapp "com.r3.corda.sdk.token:token-sdk-contract:0.1"
    cordapp "com.r3.corda.sdk.token.modules:token-sdk-money:0.1"
}

task deployNodes(type: net.corda.plugins.Cordform, dependsOn: ['jar']) {
    node {
        name "O=Notary,L=London,C=GB"
        notary = [validating : false]
        p2pPort 10002
        rpcSettings {
            address("localhost:10003")
            adminAddress("localhost:10043")
        }
        cordapps = []
    }
    node {
        name "O=PartyA,L=London,C=GB"
        p2pPort 10005
        rpcSettings {
            address("localhost:10006")
            adminAddress("localhost:10046")
        }
        cordapps = [
                "com.r3.corda.sdk.token:token-sdk-workflow:0.1",
                "com.r3.corda.sdk.token:token-sdk-contract:0.1",
                "com.r3.corda.sdk.token.modules:token-sdk-money:0.1"
        ]
        cordapp project(":contracts")
        cordapp project(":workflows")
        rpcUsers = [[ user: "user1", "password": "test", "permissions": ["ALL"]]]
    }
    node {
        name "O=PartyB,L=New York,C=US"
        p2pPort 10008
        rpcSettings {
            address("localhost:10009")
            adminAddress("localhost:10049")
        }
        cordapps = [
                "com.r3.corda.sdk.token:token-sdk-workflow:0.1",
                "com.r3.corda.sdk.token:token-sdk-contract:0.1",
                "com.r3.corda.sdk.token.modules:token-sdk-money:0.1"
        ]
        cordapp project(":contracts")
        cordapp project(":workflows")
        rpcUsers = [[ user: "user1", "password": "test", "permissions": ["ALL"]]]
    }
}
